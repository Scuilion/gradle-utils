version '0.3'

//plugins {
//  id "com.jfrog.artifactory" version "3.0.0"
//}
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}
    apply plugin: 'com.jfrog.bintray'

apply plugin: 'maven-publish'
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

apply from: 'bintray.gradle'

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.11'
}

task sourceJar(type: Jar) {
    from sourceSets.main.groovy
    from sourceSets.main.resources
    classifier = 'sources'
}

artifacts {
    archives jar, sourceJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.scuilion.gradle'
            artifactId 'utils'
            version version
            from components.java

            /*artifact sourceJar {
                classifier "sources"
            }*/
        }
    }
}

//Add funtional test
configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    integrationTestCompile.extendsFrom compile
    integrationTestRuntime.extendsFrom runtime
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
    }
}

task integrationTest(type:Test){
    dependsOn publishToMavenLocal
    dependsOn integrationTestClasses

    systemProperties['integrationTest.location'] = new File(sourceSets.integrationTest.output.resourcesDir, 'pluginTest').absolutePath
    systemProperties['version'] = version

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath + sourceSets.main.output 

    getReports().getHtml().setDestination(new File("$project.reporting.baseDir", 'integrationTest'))
    getReports().getJunitXml().setDestination(new File("$project.buildDir", 'functional-results')) 
}
integrationTest.mustRunAfter test



